<!-- REQUISITION ISSUE SLIP -->
<workunit>
    <invokers>
       <invoker type="ris:formActions" caption="Issue Items" action="create" 
        visibleWhen="#{entity?.txntype == 'AF:ISSUE' &amp;&amp; entity?.state == 'APPROVED'}" />
       
       <invoker type="formActions"  action="viewRIS" caption="View RIS" visibleWhen="#{entity?.state == 'APPROVED' || entity?.state == 'CLOSED'}" />
       <invoker type="formActions"  action="viewRIV" caption="View RIV" visibleWhen="#{entity?.state == 'APPROVED' || entity?.state == 'CLOSED'}" />
    </invokers>
    
    <code>
        <![CDATA[    
        import com.rameses.rcp.annotations.*
        import com.rameses.rcp.common.*
        import com.rameses.osiris2.client.*
        import com.rameses.osiris2.common.*
        import java.rmi.server.*;
        import com.rameses.gov.treasury.controllers.*
        
        class  CloseAFIssueController extends CRUDController {
        
            @Caller
            def caller;

            @Service("AFIssueService")
            def  service;

            @Service("AFStockQueryService")
            def  stockService;
            
            String title = "Issue Accountable Forms";
            boolean showConfirmOnSave = true;
            String confirmSaveMsg = "You are about to update the AF inventory. Continue?";            
            
            def ris;
            def selectedItem;
            boolean showSelected = false;
            boolean allowEdit = false;
            boolean allowApprove = false;
            boolean allowCreate = false;

            Map createEntity() {
                ris = caller.entity;
                def m = [objid:"AFISS"+ new UID() ];
                m.items = [];
                m.ris = ris;
                return m;
            }

            void beforeSave(o) {
                if( !entity.items )
                    throw new Exception("Please add at least one item");
            }
            
            void afterSave(o) {
                ris.state = "CLOSED";
            }
            
            def close() {
                if( mode == "read" ) 
                    return "_close:_close";
                else    
                    return "_close";
            }
            
            def reqItemModel = [
                onColumnUpdate: { item, colname->
                   if( item.qty < item.qtyissued )
                        throw new Exception("Qty to issue must be less than requested qty");
                },
                fetchList: { o->
                    return ris.items;
                }
            ] as EditorListModel;
            
            def issuedItemModel = [
                fetchList: { o->
                    if( showSelected ) {
                        return entity.items.findAll{ 
                            it.af == selectedItem?.item?.code 
                        };
                    }
                    return entity.items;
                }
            ] as BasicListModel;
            
            void loadStock() {
                if( ris.items.sum{it.qtyissued} == 0 )
                    throw new Exception("Please specify qty to issue");

                //load each item requested
                entity.items = [];
                ris.items.each {
                    try {
                        if(it.qtyissued) {
                            def list = stockService.getAvailable( 
                                [qty:it.qtyissued, af:it.item.code]
                            );
                            entity.items.addAll( list );
                            it.remarks = 'OK';
                        }
                    }
                    catch(e) {
                        it.remarks = "Not enough stock available";
                    }
                }
                issuedItemModel.reload();
                reqItemModel.reload();
            }
            
            def viewRIS() {
                return InvokerUtil.lookupOpener("report:ris", [risid:entity.ris.objid])
            }
            
            def viewRIV() {
                return InvokerUtil.lookupOpener("report:riv", [risid:entity.ris.objid])
            }            

        }
        ]]>
        
    </code>
    
    <pages>
        <page template="com.rameses.gov.treasury.ris.AFIssuePage"/>
    </pages>
    
</workunit>