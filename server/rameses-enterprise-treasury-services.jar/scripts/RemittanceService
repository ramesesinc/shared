import com.rameses.annotations.*;
import com.rameses.util.*;
import java.rmi.server.*;
import com.rameses.common.*
import com.rameses.services.extended.*

class RemittanceService  {

	@ActiveDB("remittance")
	def em; 

	@Env
	def env;

	@Service("SequenceService")
	def seqSvc;

	@Service("DateService")
	def dateSvc;

	@Service("CashBookPostService")
	def cashBook;
	

	@ProxyMethod
	public def init() { 
		//check first if there are still remaining collection on the end of the month 
		def info = [collectorid: env.USERID, txndate: getMonthFirstDate( dateSvc.serverDate ) ];
		def items = em.getUnremittedForCollector( info );
		def o = [objid: "REM"+new UID()];		
		o.message = ""
		if( items) { 
			o.items = items 
			o.endofmonth = true 
			o.message = "End of the month remittance only. Please remit again for the remaining collection."
		} else {
			info.txndate = dateSvc.serverDate
			o.message = ""
		}
		o.items = em.getUnremittedForCollector( info );
		if(! o.items)
			throw new Exception("No collections to be remitted.   ");

		o.items.each {
			it.cancelSeriesList = em.getUnremittedCancelSeries( [collectorid:it.collector.objid, controlid: it.controlid ])
		}
		o.amount = 0;
		o.totalcash = 0;
		o.totalnoncash = 0;
		if(o.items) {
			o.amount = o.items.sum{ it.amount };	
			o.totalcash = o.items.sum{ it.totalcash };	
			o.totalnoncash = o.items.sum{ it.totalnoncash };	
		}
		o.checks = em.getUnremittedChecks( info );
		o.collector = [objid:env.USERID, name:env.FULLNAME, title:env.JOBTITLE ];
		//used for checking
		o.checksum = em.getUnremittedTotals( info  );
		return o;
	}

	@ProxyMethod
	public def post( o ) {
		if( o.amount == 0 ) 
			throw new Exception("There is no amount to remit");

		if(! o.cashbreakdown ) throw new Exception("Cash breakdown has been change during the submission.Please recreate transaction");

		//compare checksum. deny if changed
		def p = [remittanceid: o.objid, collectorid: o.collector.objid, txndate : dateSvc.serverDate]
		if( o.endofmonth ) p.txndate = getMonthFirstDate( p.txndate )
		def checksum = o.checksum;
		def checktest = em.getUnremittedTotals( p );
		if(checksum!=checktest)
			throw new Exception("Changes have been made prior to remittance. Please redo transaction");

		def txncode = "REM";	
		if(env.TXNCODE) txncode = env.TXNCODE; 	
		o.txnno = txncode + seqSvc.getNextFormattedSeries(txncode);
		o.state = "OPEN";
		o.dtposted = dateSvc.serverDate;
		em.create( o );

		//insert the cash receipts
		em.collectReceipts(p);
		em.collectChecks(p);
		
		//update cashbook to add new entry
		def flist = em.getRemittedFundTotals(p);
		flist.each {
				def detail = [:];
				detail.fundid = it.fund.objid;
				detail.subacctid = o.collector.objid; 
				detail.type = 'COLLECTOR'
				detail.refid = o.objid;
				detail.refno = o.txnno;
				detail.refdate = o.dtposted;
				detail.reftype = "remittance";
				detail.amount = it.amount;
				detail.particulars = "REMITTANCE";
				cashBook.postCR( detail );

				def n = [:]
				n.objid = "REMFUND"+new UID();
				n.remittanceid = o.objid;
				n.fund = it.fund;
				n.amount = it.amount;
				em.create( n, "remittance_fund" );
		}
		return o;
	}

	@ProxyMethod
	public def open( o ) {
		o = em.read( o );
		o.checks = em.getRemittedChecks(o);
		return o;
	}

	@ProxyMethod
	public def getUnremittedReceipts(o) {
		def m = [collectorid: o.collector.objid, txndate: getMonthFirstDate( dateSvc.serverDate )];
		def list =  em.getUnremittedReceipts(m);
		if( !list ) {
			m.txndate = dateSvc.serverDate;
			list = em.getUnremittedReceipts(m);
		}
		return list;
	}

	@ProxyMethod
	public def getRemittedReceipts(o) {
		return em.getRemittedReceipts(o);
	}


	public def getMonthFirstDate( dt ) {
        Calendar cal = Calendar.getInstance();
        cal.setTime(dt);
        cal.set( Calendar.DAY_OF_MONTH, 1 );
        return dateSvc.format('yyyy-MM-dd', cal.getTime()) + " 00:00:00";
    }
}