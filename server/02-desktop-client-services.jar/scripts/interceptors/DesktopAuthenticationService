import com.rameses.annotations.*;
import com.rameses.util.*;

class DesktopAuthenticationService
{
	@Service('DateService')
	def dateSvc;

	@Service('Var')
	def var;	

	@Service('DesktopBasicUserService')
	def svc;

	@After(pattern="AuthenticationService.authenticate", eval="env.CLIENTTYPE=='desktop'") 
	public void authenticate( evt ) 
	{
		def username = evt.args[0];
		def password = evt.args[1];
		def resmap = authenticateImpl(username, password);
		evt.result.putAll(resmap);
		evt.result.HANDLER = this;
	} 

	private Map authenticateImpl( username, password ) 
	{
		def isSA = username.toString().equalsIgnoreCase("sa");
		if (isSA) {
			if (password == var.sa_pwd) return [:]; 

			throw new Exception("Access denied. Please check your username and password"); 
		}

		def info = svc.authenticate(username, password);
		
		info.pwdlogincount = toInt(info.pwdlogincount); 

		def pwd_change_login_count = toInt(var.pwd_change_login_count);
		if (pwd_change_login_count > 0) 
		{
			svc.incrementLoginCount(username, info.pwdlogincount);
			info.pwdlogincount++;
		}

		def CHANGE_PWD_ON_LOGIN = false; 
		if (pwd_change_login_count > 0 && info.pwdlogincount > pwd_change_login_count) 
			CHANGE_PWD_ON_LOGIN = true;
		else if (!info.pwdexpirydate || dateSvc.serverDate >= info.pwdexpirydate) 
			CHANGE_PWD_ON_LOGIN = true; 

		def result = [:];
		result.CHANGE_PWD_ON_LOGIN = CHANGE_PWD_ON_LOGIN;
		return result;
	} 
	
	private def toInt( value ) {
		try {
			return value.toInteger(); 
		} catch(e) {
			return 0; 
		}
	}	
}